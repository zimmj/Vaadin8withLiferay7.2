plugins {
    id "com.devsoap.plugin.vaadin" version "1.3.1" //highest plugin which is working with gradle 4.10
}
dependencies {
    compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "4.0.0"
    compileOnly group: "javax.portlet", name: "portlet-api", version: "2.0"
    compileOnly group: "javax.servlet", name: "javax.servlet-api", version: "4.0.1"
    compileOnly group: "org.osgi", name: "osgi.cmpn", version: "6.0.0"
    compileOnly group: "com.vaadin", name: "vaadin-themes", version: vaadin.version
    compileOnly group: "com.vaadin", name: "vaadin-shared", version: vaadin.version
    compileOnly group: "com.vaadin", name: "vaadin-server", version: vaadin.version
}

vaadin {
    version "8.9.0"
}

buildCSS.enabled = false //disable Liferay SCSS converter
vaadinJavadocJar.enabled = false // disable Javadoc Build
war.enabled = false // disable warbuild

// created arctifact need to be a jar for OSGI
artifacts {
    archives jar
}

repositories {
    mavenCentral()
}

// fine tune vaadin task for Liferay
vaadinCompile {
    draftCompile false
    style 'OBF'
    strict true

    manageWidgetset true //Creates Widgetset for us, can be turned of
    widgetset 'org.vaadin.liferay.example.AppWidgetSet' //location of widgetset
    //Specific amount of memory for Vaadin compile, otherwise Widgetset can not be build.
    jvmArgs '-Xmx2G'
}

jar {
    dependsOn 'vaadinCompile' // adds task to build process
    exclude('**/*.java')
    exclude('**/*.gwt.xml')
    exclude('**/test/**/*.class')
}
